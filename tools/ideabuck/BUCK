load("//tools/build_rules:java_rules.bzl", "java_test")

# =========================================================
# 'src' directory layout:
# =========================================================

# Classes considered to be stable for public use
API_SOURCE_FILES = glob([
    "src/com/facebook/buck/intellij/ideabuck/api/**/*.java",
])

# Minimal set of ideabuck files needed to compile grammar
# Keep as small as possible to permit the possibility of
# extracting a standalone grammar from ideabuck.
GRAMMAR_SOURCE_FILES = [
    "src/com/facebook/buck/intellij/ideabuck/lang/BuckFile.java",
    "src/com/facebook/buck/intellij/ideabuck/lang/BuckFileType.java",
    "src/com/facebook/buck/intellij/ideabuck/lang/BuckLanguage.java",
    "src/com/facebook/buck/intellij/ideabuck/lang/psi/BuckElementType.java",
    "src/com/facebook/buck/intellij/ideabuck/lang/psi/BuckPsiUtils.java",
    "src/com/facebook/buck/intellij/ideabuck/lang/psi/BuckTokenType.java",
]

BUCK_BNF = "src/com/facebook/buck/intellij/ideabuck/lang/Buck.bnf"

BUCK_FLEX = "src/com/facebook/buck/intellij/ideabuck/lang/Buck.flex"

# Implementation classes that should not be used by other plugins.
PLUGIN_SOURCE_FILES = glob(
    ["src/**/*.java"],
    exclude = API_SOURCE_FILES + GRAMMAR_SOURCE_FILES,
)

# =========================================================
# Grammar artifacts (JFlex lexer, GrammarKit parser)
# =========================================================

# Make the output artifact be a directory named "com"
# so when it gets included in the zip_file, it appears
# at the right prefix to be used as a Java source zip file.

genrule(
    name = "buck_parser",
    srcs = [BUCK_BNF],
    out = "com",
    cmd = (
        "mkdir $OUT && $(exe //third-party/java/grammar-kit:grammar-kit) $OUT/.. $SRCS"
    ),
)

zip_file(
    name = "buck_parser.src",
    srcs = [":buck_parser"],
)

genrule(
    name = "buck_lexer",
    srcs = [BUCK_FLEX],
    out = "com",
    cmd = (
        "$(exe //third-party/java/grammar-kit:jflex)" +
        " -skel $(location //third-party/java/grammar-kit:idea-flex.skeleton)/idea-flex.skeleton" +
        " -d $OUT/facebook/buck/intellij/ideabuck/lang" +
        " $SRCS"
    ),
)

zip_file(
    name = "buck_lexer.src",
    srcs = [":buck_lexer"],
)

java_library(
    name = "grammar",
    srcs = GRAMMAR_SOURCE_FILES + [
        ":buck_lexer.src",
        ":buck_parser.src",
    ],
    provided_deps = [
        "//third-party/java/intellij:intellij-plugin-sdk",
    ],
    resources = ["resources/icons/buck_icon.png"],
    resources_root = "resources/",
)

# ================================================================
# Classes considered to be part of ideabuck's public/reusable API.
# Other IntelliJ plugins that wish to declare a dependency on
# ideabuck to reference its classes should limit themselves to
# classes defined here.
#
# Note that the API does not have dependencies on generated
# grammar artifacts.  This is intentional to enforce that the
# public API does not leak grammar implementation details.
# ================================================================
java_library(
    name = "api",
    srcs = API_SOURCE_FILES,
    provided_deps = [
        "//third-party/java/intellij:intellij-plugin-sdk",
    ],
    source = "1.8",
    target = "1.8",
    visibility = [
        "PUBLIC",
    ],
)

java_library(
    name = "ideabuck-lib",
    srcs = PLUGIN_SOURCE_FILES,
    exported_deps = [
        ":api",
    ],
    provided_deps = [
        "//third-party/java/intellij:intellij-plugin-sdk",
    ],
    source = "1.8",
    target = "1.8",
    visibility = [
        "//tools/ideabuck/...",
    ],
    deps = [
        ":grammar",
        "//src/com/facebook/buck/event/external:external_lib",
        "//third-party/java/android:ddmlib",
        "//third-party/java/gson:gson",
        "//third-party/java/ini4j:ini4j",
        "//third-party/java/jackson:jackson-annotations",
        "//third-party/java/jackson:jackson-databind",
        "//third-party/java/jackson:jackson-datatype-guava",
        "//third-party/java/jackson:jackson-mrbean",
        "//third-party/java/jetty:jetty",
    ],
)

java_library(
    name = "resources",
    srcs = [],
    resources = glob(
        [
            "**/*.png",
            "**/*.xml",
        ],
        exclude = ["resources/META-INF/**"],
    ),
    resources_root = "resources/",
    visibility = [
        "PUBLIC",
    ],
)

java_binary(
    name = "ideabuck",
    meta_inf_directory = "resources/META-INF",
    deps = [
        ":ideabuck-lib",
        ":resources",
    ],
)

